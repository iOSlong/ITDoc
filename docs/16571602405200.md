#### BNF(巴科斯范式)

##### 巴科斯范式 以美国人巴科斯(Backus)和丹麦人诺尔(Naur)的名字命名的一种形式化的语法表示方法，用来描述语法的一种形式体系，是一种典型的元语言。又称巴科斯-诺尔形式(Backus-Naur form)。它不仅能严格地表示语法规则，而且所描述的语法是与上下文无关的。它以递归方式描述语言中的各种成分，凡遵守其规则的程序就可保证语法上的正确性。它具有语法简单，表示明确，便于语法分析和编译的特点。

##### BNF表示语法规则的方式为：非终结符用尖括号括起。每条规则的左部是一个非终结符，右部是由非终结符和终结符组成的一个符号串，中间一般以“：：=”分开。具有相同左部的规则可以共用一个左部，各右部之间以直竖“|”隔开。

所谓非终结符就是语言中某些抽象的概念不能直接出现在语言中的符号，终结符就是可以直接出现在语言中的符号。

BNF表示语法基本结构为：

<non-terminal> ::= <replacement>

non-terminal意为非终止符，就是说我们还没有定义完的东西，还可以继续由右边的replacement，也就是代替物来进一步解释、定义。

BNF关键字及含义：

*     < > : 内包含的为必选项。

* 　　[ ] : 内包含的为可选项。 

* 　　{ } : 内包含的为可重复0至无数次的项。 

* 　　|  : 表示在其左右两边任选一项，相当于"OR"的意思。 

* 　　::= : 是“被定义为”的意思 

* 　　"..." : 术语符号 

*  　　[...] : 选项，最多出现一次 

* 　　{...} : 重复项，任意次数，包括 0 次 

* 　　(...) : 分组 

* 　　|   : 并列选项，只能选一个 

* 　　斜体字: 参数，在其它地方有解释 

例子：C语言的声明语句可以用BNF这样描述： 

　　<声明语句> ::= <类型><标识符>; | <类型><标识符>[<数字>]; 

　　这一句中<声明语句>这个非终结符被定义成了两种形式（上面用|隔开的两部分），同时在这里引入了三个终结符: 分号; 左方括号[，右方括号 ]。 

　　<类型> ::= <简单类型> | <指针类型> | <自定义类型> 

　　<指针类型> ::= <简单类型> * | <自定义类型> * 

　　<简单类型> ::= int|char|double|float|long|short|void 

　　<自定义类型> ::= enum<标识符>|struct<标识符>|union<标识符>|<标识符>

　　到这里就基本上把<类型>定义清楚了。

　　<数字> ::= 0x<十六进制数字串> | 0<八进制数字串> | <十进制数字串> 

　　<十六进制数字串> ::= <十六进制数字> | <十六进制数字串><十六进制数字> 

　　<八进制数字串> ::= <八进制数字> | <八进制数字串><八进制数字> 

　　<十进制数字串> ::= <十进制数字> | <十进制数字串><十进制数字> 

　　<十六进制数字> ::= <十进制数字> | A | B | C | D | E | F 

　　<十进制数字> ::= <八进制数字> | 8 | 9 

　　<八进制数字> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 

　　到这里就把<数字>定义清楚了 

　　<标识符> ::= <字母> | <标识符> <字母数字串> 

　　<字母数字串> ::= <字母>|<十进制数字>|<字母数字串><字母>|<字母数字串><十进制数字> 

　　<字母> ::= _ | <大写字母> | <小写字母> 

　　<小写字母> ::= a|b|c|d|e|f|g|h|i|j ……  

　　<大写字母> ::= A|B|C|D|E|F|G|H|I|J …… 

　　到此为止整个声明语句就定义完了（就是说已经没有非终结符了）。

         虽然看起来很繁，但前面定义的各种非终结符都可以很容易的在别的地方重用。比如，函数声明可以定义成下面的样子： 

　　<函数声明语句> ::= <类型><标识符>(<形参表>); 

　　<形参表> ::= <类型><标识符> | <形参表>,<形参表> 

　　只用两句就描述完了，所以BNF实际上比用自然语言要简练得多（整个C语言只用一二百句就可以描述清楚），而且相当的精确，不会有自然语言中那种模棱两可的表达。如果你对BNF比较敏感的话，会发现C里面的标识符不能由数字开头，而且在C里面下划线是被当做字母看待的（也就是说能用字母的地方都可以用下划线）。

作者：zrrto
链接：https://www.jianshu.com/p/27c3f95e7509
来源：简书
著作权归作者所有。商业转载请联系作者获得授权，非商业转载请注明出处。

[百度百科BNF](https://baike.baidu.com/item/%E5%B7%B4%E7%A7%91%E6%96%AF%E8%8C%83%E5%BC%8F/1849549?fromtitle=BNF%E8%8C%83%E5%BC%8F&fromid=9540949&fr=aladdin)



